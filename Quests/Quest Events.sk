options:
	debug: false
	
	prefix: <gold>[&aFeudalism<gold>]<r>
	debugPrefix: <gold>[<yellow>Debug<gold>]<r>
	
	warn: <gold>[<yellow>WARNING<gold>]:<r>
	err: <gold>[<light red>ERROR<gold>]:<r> #User error (like wrong argument)
	err2: <gold>[<red>ERROR<gold>]:<light red> #Critical error
	
	NPC: <green>%entity's name%<r>:<r>
	You: <light green>%player%<r>:<r>
	
#How Quests work:
#There is a variable named {feu.quest.%uuid of quest%.%How much you have done, eg 1,2,3%.%uuid of player%}
#where %text% is explanation

#Check where you stand on a quest: {feu.quest.%uuid of quest%.%uuid of player%} 
#Three stages of quests: "Not started", "In progress" and "Completed"
#Events
	
on any movement:
	#entity is a player
	wait 1 tick
	if block 2 below the player is bedrock:
		set {_BlockInfo} to metadata value "BlockInfo" of block 2 below player
		#1st quest
		if "%{_BlockInfo}%" is equal to "Starter_house1":
			execute console command "house1_door %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "starter.leaveCastle":
			execute console command "starter.leaveCastle %player%"
			stop
			
		#2nd quest
		else if "%{_BlockInfo}%" is equal to "Crazy_house.house1":
			execute console command "Crazy_house.house1 %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "Crazy_house.roomExit1":
			execute console command "Crazy_house.roomExit1 %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "Crazy_house.roomExit2":
			execute console command "Crazy_house.roomExit2 %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "Crazy_house.Boss.Enter1":
			execute console command "Crazy_house.Boss.Enter1 %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "Crazy_house.EnterWrong":
			execute console command "Crazy_house.EnterWrong %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "Crazy_house.Boss.exitInnerRoom":
			execute console command "Crazy_house.Boss.exitInnerRoom %player%"
			stop
			
		#3rd quest
		else if "%{_BlockInfo}%" is equal to "theThird.wall":
			execute console command "theThird.wall %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "theThird.finalDoor_movement":
			execute console command "theThird.finalDoor_movement %player%"
			stop
		else if "%{_BlockInfo}%" is equal to "theThird.sewers_movement":
			execute console command "theThird.sewers_movement %player%"
			stop
			
on rightclick on button:
	if player's gamemode is creative:
		message "{@warn} Door open because you're in creative."
		stop
	#cancel events need to be here, therefore this can not be converted to commands
	set {_BlockInfo} to metadata value "BlockInfo" of clicked block
	#message "%{_BlockInfo}%" #Debug
	if "%{_BlockInfo}%" is equal to "Starter_house1":
		cancel event
		if {feu.quest.starter.3.%uuid of player%} is true:
			if {feu.quest.zombiesKilled.%uuid of player%} is 0:
				message "{@You} This must be the house" to player
			uncancel event
		else if {feu.quest.starter.%uuid of player%} is "Completed": 
			message "{@You} I do not want to go inside there again.." to player
		else if {feu.quest.starter.4.%uuid of player%} is true:
			message "{@You} Why would I do that? I barley survived!"
		else:
			message "{@You} That is a creepy place, I do not want to go in there without a sword!" to player
			
	else if "%{_BlockInfo}%" is equal to "Crazy_house.house1":
		cancel event
		if {feu.quest.Crazy_house.%uuid of player%} is "In progress":
			uncancel event
		else if {feu.quest.Crazy_house.%uuid of player%} is "Completed":
			uncancel event
			
	else if "%{_BlockInfo}%" is equal to "theThird.sewers_indoor":
		cancel event
		if {feu.quest.theThird.%uuid of player%} is "Completed":
			uncancel event
	else if "%{_BlockInfo}%" is equal to "theThird.sewers":
		cancel event
		if {feu.quest.theThird.6.%uuid of player%} is true:
			if player has a tripwire hook named "<dark gray>Sewers key" with lore "<light gray>Quest Item":
				set {feu.quest.theThird.6.%uuid of player%} to false
				set {feu.quest.theThird.7.%uuid of player%} to true
				heal player
				remove all tripwire hook named "<dark gray>Sewers key" with lore "<light gray>Quest Item" from player's inventory
				uncancel event
			else:
				message "{@err2} Player came too far without a key..." to console
				log "%player% Tried to enter the sewers in quest the third without a key but with {feu.quest.theThird.7.%uuid of player%} equal to true" to "quest.log"
				message "{@err2} Something went wrong! How did you manage to come so far? <r>Please contact an admin."
		else if {feu.quest.theThird.7.%uuid of player%} is true:
			uncancel event
		else if {feu.quest.theThird.%uuid of player%} is "Completed":
			uncancel event
	else if "%{_BlockInfo}%" is equal to "theThird.finalDoor":
		cancel event
		if {feu.quest.theThird.7.%uuid of player%} is true:
			set {feu.quest.theThird.7.%uuid of player%} to false
			set {feu.quest.theThird.8.%uuid of player%} to true
			message "{@You} I should report back to Lord Wilson."
			uncancel event
		else if {feu.quest.theThird.8.%uuid of player%} is true:
			uncancel event
		else if {feu.quest.theThird.%uuid of player%} is "Completed":
			uncancel event
		else:
			message "{@You} The button doesn't work.."
			
on rightclick on chest:
	cancel event
	set {_BlockInfo} to metadata value "BlockInfo" of clicked block
	#message "%{_BlockInfo}%" #Debug
	if "%{_BlockInfo}%" is equal to "starter.chest1":
		execute console command "starter.chest1 %player%"
		stop
	else if "%{_BlockInfo}%" is equal to "Crazy_house.bossLoot":
		#cannot be a command because of "location of block"
		if {feu.quest.Crazy_house.hasLootedChests.%uuid of player%} isn't true:
			set {feu.quest.Crazy_house.hasLootedChests.%uuid of player%} to true
			
		set {_Crazy_house.getLoot} to true
		loop {feu.quest.Crazy_house.lootChest.%uuid of player%::*}:
			if location of clicked block is equal to {feu.quest.Crazy_house.lootChest.%uuid of player%::%loop-index%}:
				message "{@You} I have already looted this chest."
				set {_Crazy_house.getLoot} to false
				stop
		if {_Crazy_house.getLoot} is true:
			if {feu.quest.Crazy_house.%uuid of player%} is "Completed":
				add location of clicked block to {feu.quest.Crazy_house.lootChest.%uuid of player%::*} 
				execute console command "Crazy_house.bossLoot %player%"
				stop
				
	else if "%{_BlockInfo}%" is equal to "theThird.signs":
		set {_theThird.getLoot} to true
		loop {feu.quest.theThird.plankChests.%uuid of player%::*}:
			if location of clicked block is equal to {feu.quest.theThird.plankChests.%uuid of player%::%loop-index%}:
				message "{@You} I have already looked in this chest."
				set {_theThird.getLoot} to false
				stop
		if {_theThird.getLoot} is true:
			if {feu.quest.theThird.5.%uuid of player%} is true:
				add location of clicked block to {feu.quest.theThird.plankChests.%uuid of player%::*}
				execute console command "theThird.signs %player%"
				stop
	#Has to be last
	else if {feu.permission.%uuid of player%} is "feu.tech", "feu.admin" or "feu.op":
		uncancel event
			
#on rightclick on entity:
on Citizen right click:
	target entity's name exists
	cancel event
	if "%target entity's name%" contains "Lord Willson":
		if {feu.quest.theThird.%uuid of player%} is "Completed":
			message "<green>Lord Wilson<r>:<r> There are no more quests in this demo." to player
		else if {feu.quest.Crazy_house.%uuid of player%} is "Completed":
			execute console command "NPCLordWilson_theThird %player%"
		else if {feu.quest.starter.%uuid of player%} is "Completed":
			if {feu.quest.starter.gotPotions.%uuid of player%} is false:
				if player can hold 2 potion of health:
					give 2 potion of health named "Instant Health I" to the player
					message "<green>Lord Wilson<r>:<r> Here take this." to player
					message "{@prefix} You received two minor health potions!" to player
					set {feu.quest.starter.gotPotions.%uuid of player%} to true
				else:
					message "<green>Lord Wilson<r>:<r> You can not hold two potions, talk to me again when you can." to player
				stop
				
			execute console command "NPCLordWilson_CrazyHouse %player%"
		else:
			execute console command "NPCLordWilson_starter %player%"
		
	#1st quest
	else if "%target entity's name%" contains "Blacksmith":
		execute console command "NPCBlacksmith_starter %player%"
		
	#2nd quest
	else if "%target entity's name%" contains "House Guard":
		execute console command "NPCHouseGuard_CrazyHouse %player%"
		
	#3rd quest
	else if "%target entity's name%" contains "Wall Guard":
		execute console command "NPCWallGuard %player% %event-number%"
		
	else if "%target entity's name%" contains "Survivor":
		execute console command "NPCSurvivor %player%"