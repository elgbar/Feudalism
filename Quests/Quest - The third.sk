options:
	debug: false
	
	prefix: <gold>[&aFeudalism<gold>]<r>
	debugPrefix: <gold>[<yellow>Debug<gold>]<r>
	
	warn: <gold>[<yellow>WARNING<gold>]:<r>
	err: <gold>[<light red>ERROR<gold>]:<r> #User error (like wrong argument)
	err2: <gold>[<red>ERROR<gold>]:<light red> #Critical error
	
	You: <light green>%arg-1%<r>:<r>
	
command /theThird.wall <player>:
	#action: on any movement
	executable by: console
	trigger:
		set {_name} to "<green>Wall Guard<r>:<r>"
		if {feu.quest.theThird.%uuid of arg-1%} is "In progress":
			if {feu.quest.theThird.1.%uuid of arg-1%} is true:
				#teleport arg-1 to {feu.quest.theThird.insideWall}
				message "%{_name}% The signal came from the first house on the right." to arg-1
				set {feu.quest.theThird.1.%uuid of arg-1%} to false
				set {feu.quest.theThird.2.%uuid of arg-1%} to true
		#else if {feu.quest.theThird.2.%uuid of arg-1%} is true:
		#	teleport arg-1 to {feu.quest.theThird.insideWall}
		else:
			message "%{_name}% This town is under quarantine! You can not enter without Lord Wilson permission." to arg-1
			teleport arg-1 to {feu.quest.theThird.Wall}
		
command /NPCWallGuard <player>:
	#action: on rightclick on entity
	executable by: console
	trigger:
		set {_name} to "<green>Wall Guard<r>:<r>"
		#God this language needs some logical AND and OR...
		if {feu.quest.theThird.%uuid of arg-1%} is "In progress":
			message "%{_name}% You can see the smoke coming out the chimney." to arg-1
		else:
			message "%{_name}% This town is under quarantine! You can not enter without Lord Wilson permission." to arg-1
		
command /NPCLordWilson_theThird <player>:
	#action: on rightclick on entity
	executable by: console
	trigger:
		set {_name} to "<green>Lord Wilson<r>:<r>"
		if {feu.quest.theThird.1.%uuid of arg-1%} does not exist:
			if {feu.level.%uuid of arg-1%} is less than 5:
				message "%{_name}% I need someone with more experience." to arg-1
				stop
			set {feu.quest.theThird.%uuid of arg-1%} to "In progress"
			set {feu.quest.theThird.1.%uuid of arg-1%} to true
			message "%{_name}% Hello again %arg-1%! My scouts at the wall have observed a signal from the village. Can you go and check it out?" to arg-1
		else if {feu.quest.theThird.1.%uuid of arg-1%} is true:
			message "%{_name}% Go to the wall and talk to the guards there." to arg-1
		else if {feu.quest.theThird.2.%uuid of arg-1%} is true:
			message "%{_name}% Jump over the wall!" to arg-1
		else if {feu.quest.theThird.9.%uuid of arg-1%} is true:
			set {feu.quest.theThird.%uuid of arg-1%} to "Completed"
			set {feu.quest.theThird.9.%uuid of arg-1%} to false
			message "%{_name}% You don't need to tell me anything. I've got my report from the guards." to arg-1
			wait 40 tick
			message "%{_name}% You did a very good job!" to arg-1
			if arg-1 can hold 2 potion of health:
				give 2 potion of health named "Instant Health I" to arg-1
				message "%{_name}% Take this as a reward." to arg-1
				message "{@prefix} You received /placeholder/" to arg-1
				#set {feu.quest.starter.gotPotions.%uuid of arg-1%} to true
			else:
				message "<green>Lord Wilson<r>:<r> You can not hold 2 /placeholder/, talk to me again when you can." to arg-1
			message "{@prefix} Quest complete!" to arg-1
		else:
			message "%{_name}% Hello." to arg-1
			message "{@err2} Else triggered at %{_name}% for %arg-1%, at quest theThird. See log for more info." to console
			execute console command "dqv theThird %arg-1%"
			
command /NPCSurvivor <player>:
	#action: on rightclick on entity
	executable by: console
	trigger:
		execute console command "spawnboss kiste" #He doesn't spawn another way :P
		set {_name} to "<green>Survivor<r>:<r>"
		if {feu.quest.theThird.2.%uuid of arg-1%} is true:
			set {feu.quest.theThird.2.%uuid of arg-1%} to false
			set {feu.quest.theThird.3.%uuid of arg-1%} to true
			message "%{_name}% Oh you found me! You are the first person I have seen in months!" to arg-1
			wait 50 tick
			message "%{_name}% As you saw there are a lot of zombies outside. Can you help me <gold>clear some of them out<r>?" to arg-1
		else if {feu.quest.theThird.3.%uuid of arg-1%} is true:
			message "%{_name}% Kill some of the zombies outside." to arg-1
		else if {feu.quest.theThird.4.%uuid of arg-1%} is true:
			set {feu.quest.theThird.4.%uuid of arg-1%} to false
			set {feu.quest.theThird.5.%uuid of arg-1%} to true
			message "%{_name}% Well done! You seem stronger than you look." to arg-1
			wait 20 tick
			message "%{_name}% Can you do another task for me?" to arg-1
			wait 40 tick
			message "%{_name}% I need something to board up the windows. Can you <gold>take a look around town for some plank-ish material<r>? Like the ones on the windows." to arg-1
			wait 200 tick
			message "{@You} I should probably look inside chests." to arg-1
		else if {feu.quest.theThird.5.%uuid of arg-1%} is true:
			if arg-1 has 4 signs named "<r>Plank" with lore "<light gray>Quest Item":
				set {feu.quest.theThird.5.%uuid of arg-1%} to false
				set {feu.quest.theThird.6.%uuid of arg-1%} to true
				remove all signs named "<r>Plank" with lore "<light gray>Quest Item" from arg-1's inventory
				message "%{_name}% Great! Now can I finally board up those windows." to arg-1
				wait 45 tick
				message "%{_name}% There is a reason I haven't left this town." to arg-1
				wait 45 tick 
				message "%{_name}% Not only because this is my house, but another physical reason." to arg-1
				wait 50 tick
				message "%{_name}% To get to the other side of that wall you need to walk trough the sewers." to arg-1
				wait 50 tick
				message "%{_name}% Down in the sewers there are many dangerous monsters." to arg-1
				wait 45 tick 
				message "%{_name}% To get down there you need to <gold>obtain the key, and only the janitor has it." to arg-1
				
			else:
				message "%{_name}% You need to bring me 4 planks." to arg-1
		else if {feu.quest.theThird.6.%uuid of arg-1%} is true:
			if arg-1 has a tripwire hook named "<dark gray>Sewers key" with lore "<light gray>Quest Item":
				set {feu.quest.theThird.6.%uuid of arg-1%} to false
				set {feu.quest.theThird.7.%uuid of arg-1%} to true
				message "%{_name}% That's impressive! Now There is only one way down into the sewers." to arg-1
				wait 50 tick
				message "%{_name}% <gold>Go into the maintenance building left of the well." to arg-1
				wait 40 tick
				message "%{_name}% From there on you must find your own path." to arg-1
			else:
				message "%{_name}% To get down into the sewers you need to obtain the key, but only the janitor has it." to arg-1
		else if {feu.quest.theThird.7.%uuid of arg-1%} is true:
			message "%{_name}% Go into the maintenance building left of the well." to arg-1
		else:
			message "%{_name}% Hello." to arg-1
			message "{@err2} Else triggered at %{_name}% for %arg-1%, at quest theThird. See log for more info." to console
			execute console command "dqv theThird %arg-1%"
			
command /theThird.KilledZombies1 <player>:
	#action: Killed enough zombies
	executable by: console
	trigger:
		set {_name} to "<green>Survivor<r>:<r>"
		set {feu.quest.theThird.3.%uuid of arg-1%} to false
		set {feu.quest.theThird.4.%uuid of arg-1%} to true
		message "%{_name}% I think that's enough." to arg-1
		
command /theThird.signs <player>:
	#action: On click on chest
	executable by: console
	trigger:
		if arg-1 can hold a sign:
			give arg-1 a sign named "<r>Plank" with lore "<light gray>Quest Item"
			if size of {feu.quest.theThird.plankChests.%uuid of arg-1%::*} is 1:
				message "{@You} Found a plank!" to arg-1
				stop
			if chance of 10%:
				message "{@You} Another plank. NICE!" to arg-1
			else if chance of 15%:
				message "{@You} Found a plank." to arg-1
			else if chance of 2%:
				message "{@You} Oh look, a RARE plank." to arg-1
			else:
				message "{@You} Looks like I've got another plank." to arg-1
				
			if size of {feu.quest.theThird.plankChests.%uuid of arg-1%::*} is 4:
				message "{@You} I've caught them all!" to arg-1
		else:
			message "{@You} There isn't enough space in my inventory for another plank..." to arg-1
		
command /theThird.finalDoor_movement <player>:
	#action: On any movement
	executable by: console
	trigger:
		if {feu.quest.theThird.8.%uuid of arg-1%} is true:
			stop
		else if {feu.quest.theThird.9.%uuid of arg-1%} is true:
			stop
		else if {feu.quest.theThird.%uuid of arg-1%} is "Completed":
			stop
		else:
			teleport arg-1 to {feu.quest.theThird.finalDoor}
			
command /theThird.sewers_movement <player>:
	#action: On any movement
	executable by: console
	trigger:
		if {feu.quest.theThird.7.%uuid of arg-1%} is true:
			stop
		else if {feu.quest.theThird.8.%uuid of arg-1%} is true:
			stop
		else if {feu.quest.theThird.%uuid of arg-1%} is "Completed":
			stop
		else:
			teleport arg-1 to {feu.quest.theThird.sewers_outdoor}
		
every 10 seconds:
	loop players in radius 60 around location of {feu.boss.Janitor.spawnLoc}:
		add 1 to {_players}
	if {_players} is greater than 0:
		execute console command "spawnboss janitor"
		