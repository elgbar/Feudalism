options:
	debug: false
	
	prefix: <gold>[&aFeudalism<gold>]<r>
	debugPrefix: <gold>[<yellow>Debug<gold>]<r>
	
	warn: <gold>[&aFeudalism<gold>] <yellow>WARNING:<light red>
	err: <gold>[&aFeudalism<gold>] <red>ERROR:<light red>
	
	You: <green>%arg-1%<r>:<r>
	
command /house1_door <player>:
	#action: on any movement
	
	trigger:
		if {feu.quest.starter.%arg-1%} is "Completed": 
			message "{@You} Don't want to go inside there again.." to arg-1
			teleport arg-1 to {feu.quest.starter.house1}
			stop
		else if {feu.quest.starter.4.%arg-1%} is true:
			stop
		else if {feu.quest.starter.doorAThouse1.%arg-1%} is true:			
			stop
		else:
			message "{@You} That's a creepy place, don't want go in there without a sword!" to arg-1
			teleport arg-1 to {feu.quest.starter.house1}
			
command /starter.chest1 <player>:
	#action: on rightclick on chest
	executable by: console
	trigger:				
		if {feu.quest.zombiesKilled.%arg-1%} is greater than 3:
			message "{@prefix} Quest complete! Go talk to Lord Willson" to arg-1
			set {feu.quest.starter.3.%arg-1%} to false
			set {feu.quest.starter.4.%arg-1%} to true
			set {feu.quest.starter.doorAThouse1.%arg-1%} to false
			clear {feu.quest.zombiesKilled.%arg-1%}
		else:
			message "{@You} I should first kill some more zombies." to arg-1
					
command /NPCLordWilson_starter <player>:
	#action: on rightclick on entity
	executable by: console
	trigger:
		set {_name} to "<green>Lord Wilson<r>:<r>"
		
		if {feu.quest.starter.1.%arg-1%} is true:
			set {feu.quest.starter.1.%arg-1%} to false
			set {feu.quest.starter.2.%arg-1%} to true
			message "%{_name}% Hello %arg-1%! I am Lord Willson." to arg-1
			wait 20 tick
			message "%{_name}% I need your help! But first gear up at the armoury." to arg-1		
			stop
		else if {feu.quest.starter.3.%arg-1%} is true:
			message "%{_name}% If you <gold>follow the road left <r>there is a house. Sadly the owners of this house have now passed away." to arg-1
			wait 40 tick
			message "%{_name}% Zombies have now Infested this house. I need you to <gold>clear out this house<r>. When you are finished, <gold>retrieve the items in the chest<r> and then talk to me again" to arg-1
			set {feu.quest.starter.doorAThouse1.%arg-1%} to true
			set {feu.quest.zombiesKilled.%arg-1%} to 0
			stop
		else if {feu.quest.starter.4.%arg-1%} is true:
			set {feu.quest.starter.4.%arg-1%} to false
			set {feu.quest.starter.%arg-1%} to "Completed"
			set {feu.quest.starter.doorAThouse1.%arg-1%} to false
			message "%{_name}% Thank you for your help! Here take this sword." to arg-1
			message "{@prefix} You received an iron sword!" to arg-1
			give an iron sword named "The Slayer" to the arg-1
			stop
		else if {feu.quest.starter.%arg-1%} is "In progress":
			if {feu.quest.starter.1.%arg-1%} is false:		
				message "%{_name}% You need to head to the armoury!" to arg-1
				stop
			#else if {feu.quest.starter.%arg-1%} is "Completed":
			#	message "%{_name}% Thank you for your help." to arg-1
			#	stop
		else if {feu.quest.starter.1.%arg-1%} doesn't exist:
			execute console command "quest starter %arg-1%"
		else:
			message "%{_name}% Hello."
			message "{@err} Else triggered at %{_name}% for %arg-1% for Crazy_house" to console
			stop
command /NPCBlacksmith_starter <player>:
	#action: on rightclick on entity
	executable by: console
	trigger:
		set {_name} to "<green>Blacksmith<r>:<r>"
					
		if {feu.quest.starter.2.%arg-1%} is true:
			set {feu.quest.starter.2.%arg-1%} to false
			set {feu.quest.starter.3.%arg-1%} to true
			Message "%{_name}% Here you are! We only got this old rotten stuff for now, but I recon you will manage." to arg-1
			if {feu.items.starterSword} exists:
				give {feu.items.starterSword} to arg-1
			else:
				message "{@err} starterSword doesn't exist!" to console
				give arg-1 a wooden sword named "<yellow>Old Rotten Sword"
			equip the arg-1 with leather helmet named "<yellow>Decayed Leather Helmet"
			equip the arg-1 with leather chestplate named "<yellow>Decayed Chestplate"
			equip the arg-1 with leather leggings named "<yellow>Decayed Leggings"
			equip the arg-1 with leather boots named "<yellow>Decayed Boots"
			message "%{_name}% Now go back to Lord Willson." to arg-1
			stop
		else if {feu.quest.starter.2.%arg-1%} is false:
			message "%{_name}% How did the quest go?" to arg-1
			stop
		else if {feu.quest.starter.2.%arg-1%} doesn't exist:
			message "%{_name}% You need to speak to Lord Willson first!" to arg-1
			stop
		else:
			message "%{_name}% Hello."
			message "{@err} Else triggered at %{_name}% for %arg-1%" to console
			stop
			